#!/usr/bin/env perl
use 5.14.0;
use warnings;
use File::Basename;
use File::Temp;
use JSON::PP;
use Time::Piece;

sub with_timestamp($) {
    (my $str = shift) =~ s/^/(localtime->datetime) . "\t"/egm;
    return $str;
}

sub logger($) {
    say with_timestamp shift;
}

sub load($) {
    my $file = shift;
    if (-f $file) {
        open my $fh, '<', $file
            or die with_timestamp "failed to load $file: $!";
        local $/;
        return <$fh>;
    }
}

sub save($$) {
    my ($file, $content) = @_;
    open my $fh, '>', $file or die;
    $fh->print($content);
}

sub safe_system($) {
    my $cmd = shift;

    my $stderr_file = File::Temp->new->filename;
    chomp(my $stdout = `$cmd 2> $stderr_file`);
    my $stderr = load $stderr_file;
    die with_timestamp "failed to execute: $cmd\n$stderr" if $? != 0 || $stderr;
    return $stdout;
}

sub get($$;$) {
    my ($hash, $key, $default) = @_;
    exists $hash->{$key} ? $hash->{$key} : $default;
}

sub show_status($) {
    my $status = shift;

    if (get($status, 'running') || get($status, 'stopping')) {
        # Starting
        # MountingBackupVol
        # ThinningPreBackup
        # Copying
        # LazyThinning
        # ThinningPostBackup
        logger sprintf 'Status: %s from %s',
            get($status, 'backupPhase', 'Unknown'),
            get($status, 'dateOfStateChangeFormatted', 'Unknown');
        my $p = get $status, 'progress', +{};
        logger sprintf '%s / %s EST %s',
            get($p, 'bytesFormatted', 0),
            get($p, 'totalBytesFormatted', 0),
            get($p, 'timeRemainingFormatted', 'Unknown');
        return 0;
    } elsif (defined(my $phase = get $status, 'backupPhase')) {
        logger "status => $phase";
    }
    undef;
}

sub detect_latest_backup() {
    my $t = eval { safe_system '/usr/bin/tmutil latestbackup' };
    if ($_ = $@) {
        if (/Unable to locate machine directory for host\./) {
            return 0;
        }
        die with_timestamp $@;
    }
    undef, Time::Piece->strptime(basename($t), '%F-%H%M%S');
}

sub process() {
    my ($exit_code, $latest) = detect_latest_backup;
    return $exit_code if defined $exit_code;

    if (defined $latest) {
        my $dt = $latest->datetime;
        my $file = '/tmp/run-timemachine-latest';
        my $saved = load $file;
        if (!defined $saved || $dt ne $saved) {
            logger "The latest backup: $dt";
            save $file, $dt;
        }
    }

    my $json = safe_system "$ENV{HOME}/git/dotfiles/bin/tmstatus";
    my $status = do { decode_json($json) };
    die with_timestamp $@ if $@;

    if (defined(my $exit_code = show_status $status)) {
        return $exit_code;
    }

    if (defined $latest && (time - $latest->epoch) > 12 * 60 * 60) {
        logger 'starting backup...';
        safe_system '/usr/bin/tmutil startbackup --auto';
        logger 'done!';
    }
    return 0;
}

exit process if $0 eq __FILE__;
