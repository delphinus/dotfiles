#!/usr/bin/perl

=head1 DESCRIPTION

    A tiny tool for making cue sheets

=head1 SYNOPSIS

    make-cue [filename]
    # file should be mp3/aac/m4a

    make-cue -p [filename]
    # echo the generated cue sheet

    make-cue -i [filename]
    # adds intro & outro tracks

    make-cue -e [filename]
    # no dry-run

    make-cue -eo [filename]
    # overwrite the existing cue sheet

=cut

use 5.30.0;
use warnings;
use utf8;
use feature 'signatures';
no warnings 'experimental::signatures';

use Capture::Tiny qw(capture);
use Encode qw(decode);
use Getopt::Long qw(:config posix_default no_ignore_case bundling auto_help);
use List::Util qw(reduce);
use Pod::Usage qw(pod2usage);
use Path::Tiny qw(path);

my %opt = (
    clipboard => 1,
);
GetOptions(
    \%opt, qw(
    clipboard|c
    execute|e
    intro|i
    print|p
    overwrite|o
    title_artist|t
    verbose|v
    help|h
)) or pod2usage(1);
$opt{help} and pod2usage(0);
my $mp3 = do {
    my $filename = shift // die 'set source file';
    if ($filename =~ /\.m4a$/) {
        my $original = $filename;
        $filename =~ s/\.m4a$/\.aac/;
        if (! -f $filename) {
            0 == system 'ffmpeg', '-i', $original, '-acodec', 'copy', $filename
                or die 'canno convert from m4a to aac';
        }
    }
    path($filename);
};
my ($length) = do {
    my ($out) = capture {
        0 == system 'ffprobe', '-show_entries', 'format=duration', '-v', 'quiet', '-of', 'csv=p=0', $mp3
            or die 'cannnot determine duration';
    };
    $out + 0;
};
my $cue = $mp3->parent->child(do {
    (my $tmp = $mp3->basename) =~ s/\.[^.]+$/.cue/;
    $tmp;
});
if (!$opt{print} && !$opt{overwrite} && -r $cue) {
    die 'cue file already exists. use -o to overwrite';
}

# \(\%(\\d\+:\)\?\d\d:\d\d\)\s*\(.\{-}\)\s\+[-â€“<200e>]\+\s\+\(.\{-}\)\%(\s*\[.*\)\?$,  TRACK 01 AUDIO^M    TITLE "\3"^M    PERFORMER "\2"^M    INDEX 01 \1:00
sub parse_input($input = '') {
    my $count = $opt{intro} ? 1 : 0;
    my $tracks = reduce {
        if ($opt{intro} && $count == 1) {
            push $a->@*, {
                count => 1,
                time => '00:00:00',
                artist => '',
                title => 'Intro',
            };
        }

        (my $line = $b) =~ m{
            ^
            (?:
                \d+\.
                \s+
            )?

            (?:
                (?:
                    (?<hour> \d+) :
                )?
                (?<minute> \d?\d) : (?<second> \d\d)
                \s+
            )?

            (?:
                \d+\.
                \s+
            )?+

            (?:
                (?<artist> .*?)
                \s+ [-\x{2013}\x{200e}]+ \s+
            )?
            (?<title> .*?)
            (?:
                \s*
                (?:
                    [[/] |
                    https?://
                ).*
            )?
            \s*
            $
        }x;
        my ($artist, $title) = ($+{artist}, $+{title});
        if ($opt{title_artist}) {
            ($title, $artist) = ($artist, $title);
        }
        if (defined $title) {
            push $a->@*, {
                count => ++$count,
                time => !defined $+{minute} ? undef : sprintf('%02d:%02d:00', ($+{hour} // 0) * 60 + $+{minute}, $+{second}),
                artist => $artist,
                title => $title,
            };
        } elsif ($opt{verbose}) {
            warn "cannot parse line: $line\n";
        }
        $a;
    } [], split /[\r\n]+/, $input;

    if ($opt{intro}) {
        my $t = $length - 60;
        push $tracks->@*, {
            count => $tracks->@* + 1,
            time => sprintf('%02d:%02d:00', $t / 60, $t % 60),
            artist => '',
            title => 'Outro',
        };
    }

    for my $track ($tracks->@*) {
        if (!defined $track->{time}) {
            if ($track->{count} == 1) {
                $track->{time} = '00:00:00';
            } else {
                my $t = ($length / $tracks->@*) * ($track->{count} - 1);
                $track->{time} = sprintf '%02d:%02d:00', $t / 60, $t % 60;
            }
        }
    }
    $tracks->@*;
}

my @tracks;
if ($opt{clipboard}) {
    my ($input) = capture {
        0 == system 'pbpaste' or die 'cannot execute `pbpaste`';
    };
    @tracks = parse_input(decode utf8 => $input);
    say "parsed @{[scalar @tracks]} tracks";
}

my $output = sprintf <<EOS, $mp3;
TITLE "(Title)"
FILE "%s" WAVE
EOS
for (@tracks) {
    if (defined $_->{artist}) {
        $output .= sprintf <<EOS, $_->{count}, $_->{title}, $_->{artist}, $_->{time};
  TRACK %02d AUDIO
    TITLE "%s"
    PERFORMER "%s"
    INDEX 01 %s
EOS
    } else {
        $output .= sprintf <<EOS, $_->{count}, $_->{title}, $_->{time};
  TRACK %02d AUDIO
    TITLE "%s"
    INDEX 01 %s
EOS
    }
}

if ($opt{execute}) {
    $cue->spew_utf8($output);
} else {
    say '';
    say $output;
}
