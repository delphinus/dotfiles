#!/usr/bin/perl
use 5.30.0;
use warnings;
use utf8;
use feature 'signatures';
no warnings 'experimental::signatures';

use Capture::Tiny qw(capture);
use Getopt::Long qw(:config posix_default no_ignore_case bundling auto_help);
use List::Util qw(reduce);
use Pod::Usage qw(pod2usage);
use Path::Tiny qw(path);

my %opt = (
    clipboard => 1,
);
GetOptions(
    \%opt, qw(
    clipboard|c
    execute|e
    print|p
    overwrite|o
    verbose|v
    help|h
)) or pod2usage(1);
$opt{help} and pod2usage(0);
my $mp3 = do {
    my $filename = shift // die 'set source file';
    path($filename);
};
my ($length) = do {
    my ($out) = capture {
        0 == system 'ffprobe', '-show_entries', 'format=duration', '-v', 'quiet', '-of', 'csv=p=0', $mp3
            or die 'cannnot determine duration';
    };
    $out + 0;
};
my $cue = $mp3->parent->child(do {
    (my $tmp = $mp3->basename) =~ s/\.[^.]+$/.cue/;
    $tmp;
});
if (!$opt{print} && !$opt{overwrite} && -r $cue) {
    die 'cue file already exists. use -o to overwrite';
}

# \(\%(\\d\+:\)\?\d\d:\d\d\)\s*\(.\{-}\)\s\+[-â€“<200e>]\+\s\+\(.\{-}\)\%(\s*\[.*\)\?$,  TRACK 01 AUDIO^M    TITLE "\3"^M    PERFORMER "\2"^M    INDEX 01 \1:00
sub parse_input($input = '') {
    my $count = 0;
    my $tracks = reduce {
        (my $line = $b) =~ /
            (?:
                (?:
                    (?<hour> \d+) :
                )?
                (?<minute> \d\d) : (?<second> \d\d)
            )?
            \s+
            (?<artist> .*?)
            \s+ [-\x{2013}\x{200e}] \s+
            (?<title> .*?)
            (?:
                \s*
                \[.*
            )?
            $
        /x;
        if (defined $+{artist}) {
            push $a->@*, {
                count => ++$count,
                time => !defined $+{minute} ? undef : sprintf('%d:02d:00', ($+{hour} // 0) * 60 + $+{minute}, $+{second}),
                artist => $+{artist},
                title => $+{title},
            };
        } elsif ($opt{verbose}) {
            warn "cannot parse line: $line\n";
        }
        $a;
    } [], split /\n/, $input;
    for my $track ($tracks->@*) {
        if (!defined $track->{time}) {
            if ($track->{count} == 1) {
                $track->{time} = '00:00:00';
            } else {
                my $t = ($length / $tracks->@*) * ($track->{count} - 1);
                $track->{time} = sprintf '%d:%02d:00', $t / 60, $t % 60;
            }
        }
    }
    $tracks->@*;
}

my @tracks;
if ($opt{clipboard}) {
    my ($input) = capture {
        0 == system 'pbpaste' or die 'cannot execute `pbpaste`';
    };
    @tracks = parse_input($input);
    say "parsed @{[scalar @tracks]} tracks";
}

my $output = sprintf <<EOS, $mp3;
TITLE "(Title)"
FILE "%s" WAVE
EOS
$output .= sprintf <<EOS, $_->{count}, $_->{title}, $_->{artist}, $_->{time} for @tracks;
  TRACK %02d AUDIO
    TITLE "%s"
    PERFORMER "%s"
    INDEX 01 "%s"
EOS

if ($opt{execute}) {
    $cue->spew_utf8($output);
} else {
    say '';
    say $output;
}
