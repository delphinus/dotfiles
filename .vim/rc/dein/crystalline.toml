[[plugins]]
repo     = 'rbong/vim-crystalline'
hook_add = ''' "{{{
function! StatusLine(current, width)
  let l:s = ''

  if a:current
    let l:s .= crystalline#mode() . crystalline#right_mode_sep('')
  else
    let l:s .= '%#CrystallineInactive#'
  endif
  let l:s .= ' %f%h%w%m%r '
  if a:current
    let l:s .= crystalline#right_sep('', 'Fill') . ' %{exists("*fugitive#head") ? fugitive#head() : ""}'
  endif

  let l:s .= '%='

  if a:current
    let l:s .= crystalline#left_sep('', 'Fill') . ' %{AsciiChar()} %{&paste ?"PASTE ":""}%{&spell?"SPELL ":""}'
    let l:s .= crystalline#left_mode_sep('')
  endif
  if a:width > 80
    let l:s .= ' %{&ft}[%{&fenc!=#""?&fenc:&enc}][%{&ff}] %l/%L %c%V %P '
  else
    let l:s .= ' '
  endif

  return l:s
endfunction

" use here :ascii instead of strgetchar() because the latter cannot catch
" combined characters.
" See https://stackoverflow.com/questions/23323747/vim-vimscript-get-exact-character-under-the-cursor
function AsciiChar() abort
  " if char on cursor is `Λ̊`, :ascii returns below.
  " <Λ> 923, 16進数 039b, 8進数 1633 < ̊> 778, 16進数 030a, 8進数 1412
  let tmp = execute(':ascii')
  let chars = []
  call substitute(tmp, '<.>\s\+\d\+,\s\+\S\+ \x\+,\s\+\S\+ \d\+', '\=add(chars, submatch(0))', 'g')
  if len(chars) == 0
    return ''
  endif
  let ascii = []
  for c in chars
    let m = matchlist(c, '<\(.\)>\s\+\d\+,\s\+\S\+ \(\x\+\)')
    if len(m) > 0
      call add(ascii, printf('%s %s', m[1], m[2]))
    endif
  endfor
  return join(ascii, ' ')
endfunction

function! TabLine()
  let l:vimlabel = has('nvim') ?  ' NVIM ' : ' VIM '
  return crystalline#bufferline(2, len(l:vimlabel), 1) . '%=%#CrystallineTab# ' . l:vimlabel
endfunction

let g:crystalline_enable_sep = 1
let g:crystalline_statusline_fn = 'StatusLine'
let g:crystalline_tabline_fn = 'TabLine'
let g:crystalline_theme = 'nord'

set showtabline=2
set guioptions-=e
set laststatus=2
"}}} '''
